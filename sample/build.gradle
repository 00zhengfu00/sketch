apply plugin: 'com.android.application'

def loadProperties(String fileName){
    FileInputStream inputStream = new FileInputStream(new File(fileName))
    Properties properties = new Properties()
    properties.load(inputStream)
    inputStream.close()
    for(Map.Entry property in properties){
        ext[property.key] = property.value
        println(property.key+"="+property.value)
    }
}

// 加载签名配置文件
String projectPath = projectDir;
loadProperties projectPath+'/local.properties'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 200
        versionName "2.0.0"
    }

    signingConfigs{
        release{
            storeFile file(property("sample.storeFile"))
            storePassword property('sample.storePassword')
            keyAlias property('sample.keyAlias')
            keyPassword property('sample.keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors{
        normal{
            applicationId "me.xiaopan.sketchsample"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':library')
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile(name: 'pager-sliding-strip-1.5.1', ext: 'aar')
    compile(name: 'pull_refresh_layout-1.1.1', ext: 'aar')
}

repositories{
    flatDir{
        dirs 'libs'
    }
    mavenCentral()
}
