buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def loadProperties(String fileName) {
    FileInputStream inputStream = new FileInputStream(new File(fileName))
    Properties properties = new Properties()
    properties.load(inputStream)
    inputStream.close()
    for (Map.Entry property in properties) {
        ext[property.key] = property.value
        println(property.key + "=" + property.value)
    }
}

// 加载签名配置文件
String projectPath = projectDir;
loadProperties projectPath + '/local.properties'

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion "${BUILD_TOOLS_VERSION}"

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName "${VERSION_NAME}"
    }

    signingConfigs {
        release {
            storeFile file(property("sample.storeFile"))
            storePassword property('sample.storePassword')
            keyAlias property('sample.keyAlias')
            keyPassword property('sample.keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        normal {
            applicationId "me.xiaopan.sketchsample"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':sketch')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'me.xiaopan:gohttp:1.2.0'
    compile 'me.xiaopan:androidinjector:1.5.0'
    compile 'me.xiaopan:pagerslidingtabstrip:1.6.0'
    compile 'me.xiaopan:pullrefreshlayout:1.4.0'
//    compile 'me.xiaopan:assemblyadapter:2.2.0'

    compile(name:'assemblyadapter-release', ext:'aar')

    // ButterKnifeView绑定
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // LeakCanary内存泄露监控
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    compile 'org.greenrobot:eventbus:3.0.0'
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}
